import org.jetbrains.grammarkit.tasks.GenerateLexer

// see https://github.com/JetBrains/gradle-intellij-plugin/

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.1.6'
    id "org.jetbrains.grammarkit" version "2021.1.3"
    id "com.github.ben-manes.versions" version "0.28.0"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
}

intellij {
    version = '201.6668.121'
    pluginName = 'idea-batch'
}

patchPluginXml {
    untilBuild = ''
    changeNotes = """
               <b>Changes in version 1.0.13:</b>
               <ul>
                 <li>Commenter and file type registration fixes</li>
                 <li>Minimum version 2020.1 (201.6668.121)</li>
               </ul>
               <b>Changes in version 1.0.12:</b>
               <ul>
                 <li>Decrease minimum version to support older IDE Versions)</li>
               </ul>
               <b>Changes in version 1.0.11:</b>
               <ul>
                 <li>Unset the maximum intellij version to not required rebuild (by wibotwi)</li>
                 <li>Allow installation on all JetBrains IDEs (by wibotwi)</li>
               </ul>
               <b>Changes in version 1.0.10:</b>
               <ul>
                 <li>Fix typo causing an IllegalStateException #31 (by Divyanshu Sharma)</li>
                 <li>Change to LazyRunConfigurationProducer, RunConfigurationProducer is deprecated</li>
                 <li>pluginIcon (by Connectety)</li>
                 <li>Minimum version 191.4212.41</li>
                 <li>German translation</li>
                 <li>MIT License #34</li>
               </ul>"""
}

group 'org.intellij.lang.batch'
version '1.0.13'

task generateBatchLexer(type: GenerateLexer) {
    source = "src/main/java/org/intellij/lang/batch/lexer/Batch.flex"
    targetDir = "src/main/java/org/intellij/lang/batch/lexer/"
    targetClass = "_BatchLexer"
    purgeOldFiles = true
}

task generateExpressionLexer(type: GenerateLexer) {
    source = "src/main/java/org/intellij/lang/batch/lexer/Expression.flex"
    targetDir = "src/main/java/org/intellij/lang/batch/lexer/"
    targetClass = "_ExpressionLexer"
    purgeOldFiles = true
}

