import org.jetbrains.grammarkit.tasks.GenerateLexer

// see https://github.com/JetBrains/gradle-intellij-plugin/

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.1.6'
    id "org.jetbrains.grammarkit" version "2021.1.3"
    id "com.github.ben-manes.versions" version "0.28.0"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
}

intellij {
    version = '201.6668.121'
    pluginName = 'idea-batch'
}

patchPluginXml {
    untilBuild = ''
    changeNotes = """
               <b>Changes in version 1.0.7:</b>
               <ul>
                 <li>Fixed IDEA 14 compatiblity (which broke when support for 15 was added)</li>
               </ul>
               <b>Changes in version 1.0.6:</b>
               <ul>
                 <li>Color scheme based on default IntelliJ (by Cezary Butler @ Programisci.eu)</li>
                 <li>Dracula color scheme support (by Cezary Butler @ Programisci.eu)</li>
                 <li>Fixed IDEA 14 compatiblity (which broke when support for 15 was added)</li>
               </ul>
               <b>Changes in version 1.0.5:</b>
               <ul>
                 <li>Support for Intellij IDEA 15 (by Alexander Kriegisch)</li>
               </ul>
               <b>Changes in version 1.0.4:</b>
               <ul>
                 <li>Support for Intellij IDEA 13</li>
                 <li>Support for empty configurations</li>
               </ul>"""
}

group 'org.intellij.lang.batch'
version '1.0.13'

task generateBatchLexer(type: GenerateLexer) {
    source = "src/main/java/org/intellij/lang/batch/lexer/Batch.flex"
    targetDir = "src/main/java/org/intellij/lang/batch/lexer/"
    targetClass = "_BatchLexer"
    purgeOldFiles = true
}

task generateExpressionLexer(type: GenerateLexer) {
    source = "src/main/java/org/intellij/lang/batch/lexer/Expression.flex"
    targetDir = "src/main/java/org/intellij/lang/batch/lexer/"
    targetClass = "_ExpressionLexer"
    purgeOldFiles = true
}

